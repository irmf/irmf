/*{
  "author": "Glenn M. Lewis",
  "copyright": "Apache-2.0",
  "date": "2019-07-13",
  "irmf": "1.0",
  "materials": ["metal", "dielectric"],
  "max": [25,25,60],
  "min": [-25,-25,-60],
  "notes": "The IRMF shader that started it all... the bifilar electromagnet.",
  "options": {},
  "title": "bifilar electromagnet",
  "units": "mm",
  "version": "1.0"
}*/

#define M_PI 3.1415926535897932384626433832795

float coilSquareFace(in mat4 xfm, float radius, float size, float gap, float nTurns, in vec3 xyz) {
  xyz = (vec4(xyz, 1.0) * xfm).xyz;
  
  // First, trivial reject on the two ends of the coil.
  if (xyz.z < -0.5 * size || xyz.z > nTurns * (size + gap) + 0.5 * size) { return 0.0; }
  
  // Then, constrain the coil to the cylinder with wall thickness "size":
  float rxy = length(xyz.xy);
  if (rxy < radius - 0.5 * size || rxy > radius + 0.5 * size) { return 0.0; }
  
  // If the current point is between the coils, return no material:
  float angle = atan(xyz.y, xyz.x) / (2.0 * M_PI);
  if (angle < 0.0) { angle += 1.0; } // 0 <= angle <= 1 between coils
  float dz = mod(xyz.z, size + gap); // 0 <= dz <= (size+gap) between coils.
  
  float lastHelixZ = angle * (size + gap);
  if (lastHelixZ > dz) { lastHelixZ -= (size + gap); }
  float nextHelixZ = lastHelixZ + (size + gap);
  
  if (dz > lastHelixZ + 0.5 * size && dz < nextHelixZ - 0.5 * size) { return 0.0; }
  
  // If the current point is within start of the first coil, stop it at angle < 0.
  if (xyz.z < 0.5 * size && angle > 0.5) { return 0.0; }
  // If the current point is with the end of the last coil, stop it at angle > PI.
  if (xyz.z > nTurns * (size + gap) - 0.5 * size && angle < 0.5) { return 0.0; }
  
  return 1.0;
}

vec2 bifilarElectromagnet(float nTurns, in vec3 xyz) {
  float coil01 = coilSquareFace(mat4(1), 3.0, 0.85, 0.15, nTurns, xyz);
  float coil02 = coilSquareFace(mat4(1), 4.0, 0.85, 0.15, nTurns, xyz);
  float coil03 = coilSquareFace(mat4(1), 5.0, 0.85, 0.15, nTurns, xyz);
  float coil04 = coilSquareFace(mat4(1), 6.0, 0.85, 0.15, nTurns, xyz);
  float coil05 = coilSquareFace(mat4(1), 7.0, 0.85, 0.15, nTurns, xyz);
  float coil06 = coilSquareFace(mat4(1), 8.0, 0.85, 0.15, nTurns, xyz);
  float coil07 = coilSquareFace(mat4(1), 9.0, 0.85, 0.15, nTurns, xyz);
  float coil08 = coilSquareFace(mat4(1), 10.0, 0.85, 0.15, nTurns, xyz);
  float coil09 = coilSquareFace(mat4(1), 11.0, 0.85, 0.15, nTurns, xyz);
  float coil10 = coilSquareFace(mat4(1), 12.0, 0.85, 0.15, nTurns, xyz);
  float coil11 = coilSquareFace(mat4(1), 13.0, 0.85, 0.15, nTurns, xyz);
  float coil12 = coilSquareFace(mat4(1), 14.0, 0.85, 0.15, nTurns, xyz);
  float coil13 = coilSquareFace(mat4(1), 15.0, 0.85, 0.15, nTurns, xyz);
  float coil14 = coilSquareFace(mat4(1), 16.0, 0.85, 0.15, nTurns, xyz);
  float coil15 = coilSquareFace(mat4(1), 17.0, 0.85, 0.15, nTurns, xyz);
  float coil16 = coilSquareFace(mat4(1), 18.0, 0.85, 0.15, nTurns, xyz);
  float coil17 = coilSquareFace(mat4(1), 19.0, 0.85, 0.15, nTurns, xyz);
  float coil18 = coilSquareFace(mat4(1), 20.0, 0.85, 0.15, nTurns, xyz);
  float coil19 = coilSquareFace(mat4(1), 21.0, 0.85, 0.15, nTurns, xyz);
  float coil20 = coilSquareFace(mat4(1), 22.0, 0.85, 0.15, nTurns, xyz);
  
  float metal = coil01 + coil02 + coil03 + coil04 + coil05 + coil06 +
  coil07 + coil08 + coil09 + coil10 + coil11 + coil12 + coil13 +
  coil14 + coil15 + coil16 + coil17 + coil18 + coil19 + coil20;
  
  float dielectric = 0.0;
  
  return vec2(metal, dielectric);
}

void mainModel4(out vec4 materials, in vec3 xyz) {
  xyz.z += 60.0;
  materials.xy = bifilarElectromagnet(120.0, xyz);
}
